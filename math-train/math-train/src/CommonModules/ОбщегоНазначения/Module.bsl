
Функция СлучайноеЧисло(НижняяГраница, ВерхняяГраница) Экспорт

	ГСЧ = ГСЧ();
	Возврат ГСЧ.СлучайноеЧисло(НижняяГраница, ВерхняяГраница);

КонецФункции

Функция ГСЧ()

	УИД = Строка(Новый УникальныйИдентификатор);
	СтрЧисло = "";
	
	Для ит=1 По 36 Цикл
	
		Код = КодСимвола(УИД, ит);
		Если Код > 47 И Код < 58 Тогда
			СтрЧисло = СтрЧисло + Сред(УИД, ит, 1);
		КонецЕсли; 
	
	КонецЦикла; 	
	
	Возврат Новый ГенераторСлучайныхЧисел(Число(СтрЧисло));
	
КонецФункции

Функция ПолучитьДанныеПримера(МаксЧисло, ВидТренировки) Экспорт

	Результат = Новый Структура();

	Результат.Вставить("Число0", ПолучитьЧисло0(ВидТренировки, МаксЧисло));
	Результат.Вставить("Число1", ПолучитьЧисло1(ВидТренировки, МаксЧисло, Результат.Число0));
	Результат.Вставить("ТекстПримера", ПолучитьТекстПримера(Результат.Число0, Результат.Число1, ВидТренировки));
	Результат.Вставить("ПравильныйОтвет", ПолучитьПравильныйОтвет(Результат.Число0, Результат.Число1, ВидТренировки));
	Результат.Вставить("Варианты", ПолучитьВарианты(Результат.ПравильныйОтвет, МаксЧисло, ВидТренировки));

	Возврат Результат;

КонецФункции

Функция ПолучитьЧисло0(ВидТренировки, МаксЧисло)
	
	Результат = Неопределено;
	
	Если ВидТренировки = Перечисления.ВидТренировки.Сложение
		ИЛИ ВидТренировки = Перечисления.ВидТренировки.Вычитание Тогда
		Результат = СлучайноеЧисло(0, МаксЧисло);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ФорматЧисел()
	
	Возврат "ЧН=0; ЧГ=;";
	
КонецФункции

Функция ПолучитьЧисло1(ВидТренировки, МаксЧисло, Число0)
	
	Результат = Неопределено;
	
	Если ВидТренировки = Перечисления.ВидТренировки.Сложение Тогда
		Результат = СлучайноеЧисло(0, МаксЧисло);
	ИначеЕсли ВидТренировки = Перечисления.ВидТренировки.Вычитание Тогда 
		Результат = СлучайноеЧисло(0, Число0);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьТекстПримера(Число0,Число1,ВидТренировки)
	
	Возврат Формат(Число0, ФорматЧисел()) 
	+ ПолучитьПредставлениеДействия(ВидТренировки) 
	+ Формат(Число1, ФорматЧисел());
	
КонецФункции

Функция ПолучитьПредставлениеДействия(ВидТренировки)
	
	Результат = "";
	
	Если ВидТренировки = Перечисления.ВидТренировки.Сложение Тогда
		Результат = " + ";	
	ИначеЕсли ВидТренировки = Перечисления.ВидТренировки.Вычитание Тогда 
		Результат = " - ";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьМаксимум(ВидТренировки, МаксЧисло)
	
	Результат = Неопределено;
	
	Если ВидТренировки = Перечисления.ВидТренировки.Сложение Тогда
		Результат = МаксЧисло*2;	
	ИначеЕсли ВидТренировки = Перечисления.ВидТренировки.Вычитание Тогда 
		Результат = МаксЧисло;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьПравильныйОтвет(Число0,Число1,ВидТренировки)
	
	Результат = Неопределено;
	
	Если ВидТренировки = Перечисления.ВидТренировки.Сложение Тогда
		Результат = Число0 + Число1;	
	ИначеЕсли ВидТренировки = Перечисления.ВидТренировки.Вычитание Тогда 
		Результат = Число0 - Число1;
	КонецЕсли;
	
	Возврат Формат(Результат, ФорматЧисел());
	
КонецФункции

Функция ПолучитьВарианты(ПравильныйОтвет, МаксЧисло, ВидТренировки)
	
	Максимум = ПолучитьМаксимум(ВидТренировки, МаксЧисло);
	
	Варианты = Новый Массив;
	Варианты.Добавить(ПравильныйОтвет);
	
	Ит = 0;
	
	Пока Варианты.Количество() < 4 Цикл
		
		СЧ = Формат(ОбщегоНазначения.СлучайноеЧисло(0, Максимум), ФорматЧисел());
		
		Если Варианты.Найти(СЧ) = Неопределено Тогда
			Варианты.Добавить(СЧ);	
		КонецЕсли;	
		
		Ит = Ит + 1;
		
		Если Ит > 10000 Тогда
			ВызватьИсключение "Ошибка при получении вариантов";
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Варианты;
	
КонецФункции